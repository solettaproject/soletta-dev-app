#!/usr/bin/env sol-fbp-runner
#
# This example showcases persistence usage. By clicking on Buttons 1 & 2,
# one can increase/decrease a counter, that is displayed on 7 seg display.
# Each change is also persisted to Calamari EEPROM, so this sample will, on
# a second run, remembers counter last value.
# Note that on sol-flow.json in this directory is defined the memory map used
# to store information on EEPROM. Property 'path' contains the instructions
# to 'create' the i2c device. Alternatively, it could be a path to EEPROM file
# on sysfs, considering it's already created. In this case, path would be
# '/sys/bus/i2c/devices/7-0050/eeprom'
# Note that to handle EEPROM initial value being 0xff (255), we catch accumulator
# ERROR packet to reset value to zero. Accumulator will send an error packet
# because it will receive a value (255) out of its range [0-15], so we can
# handle properly.

btn1(Button1)
btn2(Button2)
accumulator(int/accumulator:send_initial_packet=false,setup_value=min:0|max:15|step:1)
seg(SevenSegments)
persistence(persistence/int:storage="memmap",name="accumulated",default_value=0)

persistence OUT -> SET accumulator
persistence OUT -> VALUE seg

btn1 OUT -> IN _(boolean/filter) TRUE -> INC accumulator
btn2 OUT -> IN _(boolean/filter) TRUE -> DEC accumulator

accumulator OUT -> IN persistence
accumulator ERROR -> RESET accumulator
